buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {

    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.5"
}


project.ext {
    librarySubProjects = subprojects.findAll{
        it.name == 'gdxtween' || it.name == 'gdxtween-kt'
    }
}

subprojects {
    apply plugin: "idea"
    apply plugin: "java"

    group = 'com.cyphercove.gdxtween'
    version = '0.1.4'

    ext {
        gdxVersion = '1.9.11'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    dependencies {
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

configure(project.librarySubProjects) {
    apply plugin: "java-library"

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        publications {
            "$projectId"(MavenPublication) {
                groupId = project.group
                artifactId = projectId
                version = project.version
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = projectId
                    description = projectDescription
                    url = 'https://github.com/CypherCove/gdx-tween'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    organization {
                        name = 'Cypher Cove'
                        url = 'www.cyphercove.com'
                    }
                    scm {
                        connection = 'scm:git:git@github.com:cyphercove/gdx-tween.git'
                        developerConnection = 'scm:git:git@cyphercove/gdx-tween.git'
                        url = 'https://github.com/cyphercove/gdx-tween/'
                    }
                }
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = [projectId]
        pkg {
            repo = 'maven'
            name = projectId
            userOrg = 'cyphercove'
            version {
                name = version
                released  = new Date()
            }
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/cyphercove/gdx-tween/'
        }
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}

