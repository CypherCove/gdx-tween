plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

project.ext {
    librarySubProjects = subprojects.findAll{
        it.name == 'gdxtween' || it.name == 'gdxtween-kt'
    }
}

subprojects {
    apply plugin: "idea"
    apply plugin: "java"

    group = libraryGroup
    version = libraryVersion

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = "https://jitpack.io" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    dependencies {
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

configure(project.librarySubProjects) {
    apply plugin: "java-library"

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        publications {
            "$projectId"(MavenPublication) {
                groupId = libraryGroup
                artifactId = projectId
                version = libraryVersion
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = projectId
                    description = projectDescription
                    url = 'https://github.com/CypherCove/gdx-tween'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    organization {
                        name = 'Cypher Cove'
                        url = 'www.cyphercove.com'
                    }
                    developers {
                        developer {
                            id = 'cypher-cove'
                            name = 'Cypher Cove'
                            email = 'contact@cyphercove.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:cyphercove/gdx-tween.git'
                        developerConnection = 'scm:git:git@cyphercove/gdx-tween.git'
                        url = 'https://github.com/cyphercove/gdx-tween/'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications."$projectId"
    }

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}

